       def main

**
* Constants
*
screen_width:
       equ  32
screen_height:
       equ  24
map_width:
       equ  256
map_height:
       equ  512
map_width_char:
       equ  32
map_height_char:
       equ  64
ball_radius:
       equ  7
scroll_y_max:
       equ  map_height-192-2
coll_map_bank_height:
       equ  32
coll_map_bank:
       equ  >6008
coll_bank_base:
       equ  >6000

**
* Memory mapped addresses
*
vdpwd  equ  >8c00                      ; VDP write data
vdpwa  equ  >8c02                      ; VDP set read/write address
vdprd  equ  >8800                      ; VDP read data
vdpsta equ  >8802                      ; VDP status
sound  equ  >8400

**
* VDP memory map
*
ptrntb equ  >0000                      ; Pattern table base x 4
sprptb equ  >2000                      ; Sprite pattern table base
colrtb equ  >2b80                      ; Color table base
nametb equ  >2800                      ; Name table base x 2
spratb equ  >2b00                      ; Sprite attribute table base

**
* Scratch pad
*
pad    equ  >8300
wrksp  equ  pad                        ; Workspace
r0lb   equ  wrksp+1                    ; r0 low byte required for VDP routines
r1lb   equ  wrksp+3
r2lb   equ  wrksp+5
r3lb   equ  wrksp+7
plyrws equ  wrksp+>20                  ; Sound player work space
stack  equ  plyrws+>20
frame:
       equ  stack+>10
scroll_y:
       equ  frame+2
x:                                     ; Ball position x (FP 12.4)
       equ  scroll_y+2
y:                                     ; Ball position y (FP 12.4)
       equ  x+2
vx:                                    ; Ball velocity x (FP 8.8)
       equ  y+2
vy:                                    ; Ball velocity y (FP 8.8)
       equ  vx+2
ax:                                    ; Ball acceleration x (FP 8.8)
       equ  vy+2
ay:                                    ; Ball acceleration y (FP 8.8)
       equ  ax+2
nx:                                    ; Normal vector x (FP 8.8)
       equ  ay+2
ny:                                    ; Normal vector y (FP 8.8)
       equ  nx+2
rx:                                    ; Reflection vector x (FP 8.8)
       equ  ny+2
ry:                                    ; Reflection vector y (FP 8.8)
       equ  rx+2
color:
       equ  ry+2

**
* Macros
*
       .defm proc
       mov  r11,*r10+
       .endm

       .defm endproc
       dect r10
       mov  *r10,r11
       b    *r11
       .endm

********************************************************************************
*
* Main program
*
       aorg >a000
main:
       limi 0                          ; Interrupts off
       lwpi wrksp                      ; Setup workspace
       li   r10,stack                  ; Setup stack pointer
       bl   @graphics_mode
       bl   @init_vdp_ram
       bl   @init_game
*      Loop
loop:
       bl   @vsync
       bl   @display_ball
       bl   @scroll_screen
       bl   @move_ball
       bl   @check_collision
       bl   @move_ball
       bl   @check_collision
       bl   @move_ball
       bl   @check_collision
       bl   @move_screen
       bl   @joystick
       jmp  loop

*********************************************************************************
*
* Initialize game
*
init_game:
       .proc
       clr  @scroll_y
       li   r0,init_values
       mov  *r0+,@x
       mov  *r0+,@y
       mov  *r0+,@vx
       mov  *r0+,@vy
       mov  *r0+,@ax
       mov  *r0+,@ay
       li   r0,>000e
       mov  r0,@color
       .endproc
init_values:
       data >0e80,>0600,>0000,->0000,>0000,>0001
*// init_game

*********************************************************************
*
* Wait for vertical retrace
*
vsync:
       movb @vdpsta,r12
vsync_1:
       movb @vdpsta,r12
       jgt  vsync_1
       jeq  vsync_1
       rt
*// vsync

*********************************************************************************
*
* Display ball
*
display_ball:
       .proc
       li   r0,spratb
       bl   @vwad
       mov  @y,r0
       srl  r0,4
       s    @scroll_y,r0
       swpb r0
       movb r0,@vdpwd
       mov  @x,r0
       sla  r0,4
       movb r0,@vdpwd
       movb @color,@vdpwd
       movb @color+1,@vdpwd
       .endproc
*// display_ball

*********************************************************************************
*
* Move ball
*
move_ball:
*      Accellerate
       a    @ax,@vx
       a    @ay,@vy
*      Move x
       mov  @vx,r0
       sra  r0,4
       mov  @x,r1
       a    r0,r1
       jlt  move_ball_1
       ci   r1,(map_width-8)*16
       jgt  move_ball_1
       mov  r1,@x
       jmp  move_ball_3
move_ball_1:
       neg  @vx
move_ball_3:
*      Move y
       mov  @vy,r0
       sra  r0,4
       mov  @y,r1
       a    r0,r1
       jlt  move_ball_2
       ci   r1,(map_height-8)*16
       jgt  move_ball_2
       mov  r1,@y
       jmp  move_ball_4
move_ball_2:
       neg  @vy
move_ball_4:
       rt
*// move_ball

*********************************************************************************
*
* Move screen
*
move_screen:
       mov  @y,r0
       srl  r0,4
       s    @scroll_y,r0
*      Should we scroll up?
       ci   r0,48
       jgt  move_screen_0
       mov  @scroll_y,r1
       ci   r1,0
       jle  move_screen_0
*      Yes - scroll up
       dect @scroll_y
       ci   r0,32
       jgt  move_screen_1
       dect @scroll_y
       jmp  move_screen_1
*      Should we scroll down?
move_screen_0:
       ci   r0,144
       jlt  move_screen_1
       mov  @scroll_y,r1
       ci   r1,scroll_y_max
       jhe  move_screen_1
*      Yes - scroll down
       inct @scroll_y
       ci   r0,160
       jlt  move_screen_1
       inct @scroll_y
move_screen_1:
       rt
*// move_screen

*********************************************************************************
*
* Scroll screen to pixel position in @scroll_y (only even positions supported)
*
scroll_screen:
       .proc
*      Pattern table
       mov  @scroll_y,r0               ; Scroll position
       andi  r0,>0006                  ; Only offsets 0, 2, 4, 6 supported
       srl  r0,1                       ; Pattern table no
       ori  r0,>0400                   ; OR by pattern table reg no
       bl   @vwtr                      ; Set pattern table
*      Name table
       li   r0,nametb                  ; VDP write address
       mov  @scroll_y,r1               ; Scroll position
       srl  r1,3                       ; Convert to character rows
       mpy  @MS0,r1                    ; Multiply by map character width
       mov  r2,r1                      ; Move LSW of result back
       ai   r1,MD0+map_width_char      ; Add map base address
       li   r3,screen_height           ; Screen height in rows
       li   r4,vdpwd                   ; Put VDP write data in reg
update_screen_1:
       bl   @vwad                      ; Setup write address
       li   r2,map_width_char/4        ; Map width, 4 times unrolled
update_screen_2:
       movb *r1+,*r4                   ; Send by to VDP
       movb *r1+,*r4                   ; ...
       movb *r1+,*r4                   ; ...
       movb *r1+,*r4                   ; ...
       dec  r2                         ; Column group counter
       jne  update_screen_2            ; Column loop
       ai   r0,screen_width            ; Address of next row
       dec  r3                         ; Row counter
       jne  update_screen_1            ; Row loop
       .endproc
*// scroll_screen


*********************************************************************************
*
* Check for collision and rebounce ball according to normal vector.
*
* Modifies r0-r3
*
check_collision:
       .proc
       bl   @check_ball_collision
       mov  r1,r1
       jeq  check_collision_1
       dec  r1                         ; Convert to zero-based
       bl   @get_normal
       bl   @calc_dot_product          ; r3=dot product of v and n
       mov  r3,r3
       jgt  check_collision_1          ; If positive, the ball is inside the wall moving out
       jeq  check_collision_1          ; If zero, the ball is moving along the wall
       bl   @reflect
       mov  @rx,@vx
       mov  @ry,@vy
check_collision_1:
       .endproc
*// check_collision

*********************************************************************************
*
* Get collision map value for ball
*
* Returns value in r0
*
* Modifies r0-r3
*
check_ball_collision:
       .proc
       mov  @x,r0
       srl  r0,4
       ai   r0,ball_radius             ; Center x
       mov  @y,r1
       srl  r1,4
       ai   r1,ball_radius             ; Center y
       bl   @get_coll_value
       .endproc
*// check_ball_collision

*********************************************************************************
*
* Get collision map value
*
* r0:  X coordinate
* r1:  Y coordinate
*
* Returns value in r1
*
* The ROM contains the collision map split into banks of 8K.
* Each bank contains 48 lines of 160 pixels of data ordered from top to bottom.
* Each pixel is represented by one byte, which determines the normal angle (1-255),
* or 0 if there is no collision.
* The data start at bank offset coll_bank_offset = >200.
* The first bank is selected by writing to @coll_map_bank.
*
* Modifies r0-r2
*
get_coll_value:
       mov  r1,r2                      ; Move y to LSW
       clr  r1                         ; Clear MSW
       div  @w_coll_map_bank_height,r1 ; Bank no in r1, line offset in r2
       sla  r1,1                       ; Convert to bank offset
       ai   r1,coll_map_bank           ; Add base bank offset
       seto *r1                        ; Select bank
       mov  r2,r1                      ; line offset
       mpy  @w_map_width,r1            ; Multiply line width and line offset
       a    r0,r2                      ; Add X
       ai   r2,coll_bank_base          ; Add ROM bank base address
       clr  r1                         ; Clear result reg
       movb *r2,@r1lb                  ; Get byte
       rt
*// get_coll_value

*********************************************************************************
*
* Get normal vector at the given angle 0-254 in FP 8.8 format.

* An angle of 0 corresponds to the normal pointing down (0,-1) and for
* increasing angles the normals are rotated in a clockwise direction.
* 0 points down, 64 points left, 128 points up, and 192 points right.
*
* r1: angle (0-254)
*
* On return @nx contains the X coordinate and @ny contains the Y coordinate.
*
get_normal:
       sla  r1,2
       ai   r1,normals
       mov  *r1+,@nx
       mov  *r1,@ny
       rt
*// get_normal

*********************************************************************************
*
* Calculate the reflection vector r of vector v around normal vector n in FP 8.8
* format.
*
* r = v − 2(v ⋅ n) n
*
* Input:
* r3: dot product of v and n (v ⋅ n)
* @vx: vx
* @vy: vy
* @nx: nx
* @ny: ny
*
* On return @rx, @ry contain the reflection vector in in FP 4.12 format.
*
* Modified r0-r3
*
reflect:
       .proc
       sla  r3,1                       ; Multiply dot product by 2
*      x
       mov  r3,r0
       mov  @nx,r1
       bl   @calc_signed_product       ; r0=2*dot product*nx
       neg  r0                         ; r0=-(2*dot product*nx)
       a    @vx,r0                     ; r0=vx-(2*dot product*nx)
       mov  r0,@rx
*      y
       mov  r3,r0
       mov  @ny,r1
       bl   @calc_signed_product       ; r0=2*dot product*ny
       neg  r0                         ; r0=-(2*dot product*ny)
       a    @vy,r0                     ; r0=vy-(2*dot product*ny)
       mov  r0,@ry
       .endproc
*// reflect

*********************************************************************************
*
* Calculate the (scalar) dot product of two vectors v and n in FP 8.8 format.
*
* v ⋅ n = vx * nx + vy * ny
*
* Input:
* @vx: vx
* @vy: vy
* @nx: nx
* @ny: ny
*
* On return r3 contains the result in FP 8.8 format.
*
* Modifies r0-r2
*
calc_dot_product:
       .proc
       mov  @vx,r0
       mov  @nx,r1
       bl   @calc_signed_product
       mov  r0,r3
       mov  @vy,r0
       mov  @ny,r1
       bl   @calc_signed_product
       a    r0,r3
       .endproc
*// calc_dot_product

*********************************************************************************
*
* Multiply two signed numbers in FP 8.8 format.
*
* Input:
* r0: first number
* r1: second number
*
* On return r0 contains the result in FP 8.8 format.
*
* Modifies r0-r2
*
calc_signed_product:
       clr  r2                         ; Sign
       mov  r0,r0                      ; Test sign of first number
       jgt  !
       inv  r2                         ; Flip sign
       neg  r0                         ; Make positive
!      mov  r1,r1                      ; Test sign of second number
       jgt  !
       inv  r2                         ; Flip sign
       neg  r1                         ; Make positive
!      mpy  r1,r0                      ; Multiply
       swpb r0                         ; Move MSB into position
       movb r1,@r0lb                   ; Move LSB into position
       mov  r2,r2                      ; Test sign
       jeq  !
       neg  r0                         ; Negate if sign is minus
!      rt
*// calc_signed_product

*********************************************************************************
*
* Check joystick
*
* Modifies r0,r12
*
joystick:
       .proc
       li   r0,JOY_LT
       bl   @check_key
       jeq  joystick_1
       li   r0,->0040
       mov  r0,@vx
       li   r0,->0040
       mov  r0,@vy
       jmp  joystick_2
joystick_1:
       li   r0,JOY_RT
       bl   @check_key
       jeq  joystick_2
       li   r0,>0040
       mov  r0,@vx
       li   r0,->0040
       mov  r0,@vy
joystick_2:
       .endproc
*// joystick

*********************************************************************************
*
* Initialize
*
init_vdp_ram:
       .proc
       clr  r0
       clr  r1
       li   r2,>4000
       bl   @vsmw
*      Patterns
       li   r0,ptrntb
       li   r1,VPFRM3
       mov  @TCHNUM,r2
       sla  r2,3
       bl   @vmbw
       li   r0,ptrntb+>800
       li   r1,VPFRM2
       mov  @TCHNUM,r2
       sla  r2,3
       bl   @vmbw
       li   r0,ptrntb+>1000
       li   r1,VPFRM1
       mov  @TCHNUM,r2
       sla  r2,3
       bl   @vmbw
       li   r0,ptrntb+>1800
       li   r1,VPFRM0
       mov  @TCHNUM,r2
       sla  r2,3
       bl   @vmbw
*      Sprite patterns
       li   r0,sprptb
       li   r1,sprite_patterns
       li   r2,32
       bl   @vmbw
*      Sprite attributes
       li   r0,spratb
       li   r1,sprite_attributes
       li   r2,4
       bl   @vmbw
       li   r1,>d000
       movb r1,@vdpwd
*      Colors
       li   r0,colrtb
       li   r1,>1f00
       li   r2,32
       bl   @vsmw
       li   r1,>0e00
       movb r1,@vdpwd
*      Name table
       li   r0,nametb
       li   r1,>ff00
       li   r2,>300
       bl   @vsmw
       .endproc
*// init_vdp_ram

*********************************************************************************
*
* Set graphics mode
*
graphics_mode:
       .proc
       clr  r0
       li   r1,vregs2
       li   r2,8
graphics_mode_1:
       movb *r1+,@r0lb
       bl   @vwtr                      ; Set register
       ai   r0,>0100
       dec  r2
       jne  graphics_mode_1
*      Return
       .endproc
*      VDP registers
vregs2:
       byte >00                        ; Graphics I mode
       byte >c2                        ; 16K, display on, interrupt disabled
       byte nametb/>400                ; Name table
       byte colrtb/>40                 ; Color table
       byte ptrntb/>800                ; Pattern table
       byte spratb/>80                 ; Sprite attribute table
       byte sprptb/>800                ; Sprite pattern table
       byte >0e                        ; Backdrop color
*// graphics_mode

*********************************************************************
*
* VDP Set Write Address
*
* R0: Write address in VDP RAM
*
* R0 is modified, but can be restored with: ANDI R0,>3FFF
*
vwad:
       swpb r0
       movb r0,@vdpwa                  ; Send low byte of VDP RAM write address
       swpb r0
       ori  r0,>4000                   ; Set read/write bits 14 and 15 to write (01)
       movb r0,@vdpwa                  ; Send high byte of VDP RAM write address
       rt
*// vwad

*********************************************************************
*
* VDP Write To Register
*
* R0 MSB: VDP register to write to
* R0 LSB: Value to write
*
vwtr:
       swpb r0
       movb r0,@vdpwa                  ; Send low byte (value) to write to VDP register
       swpb r0
       ori  r0,>8000                   ; Set up a VDP register write operation (10)
       movb r0,@vdpwa                  ; Send high byte (address) of VDP register
       rt
*// vwtr


*********************************************************************
*
* VDP Single Byte Write
*
* R0: Write address in VDP RAM
* R1: MSB of R1 sent to VDP RAM
*
* R0 is modified, but can be restored with: ANDI R0,>3FFF
*
vsbw:
       swpb r0
       movb r0,@vdpwa                  ; Send low byte of VDP RAM write address
       swpb r0
       ori  r0,>4000                   ; Set read/write bits 14 and 15 to write (01)
       movb r0,@vdpwa                  ; Send high byte of VDP RAM write address
       movb r1,@vdpwd                  ; Write byte to VDP RAM
       rt
*// vsbw

*********************************************************************
*
* VDP Single byte Multiple Write
*
* R0: Write address in VDP RAM
* R1: Byte to write in MSB
* R2: Number of bytes to write
*
* R0 is modified, but can be restored with: ANDI R0,>3FFF
*
vsmw:
       swpb r0
       movb r0,@vdpwa                  ; Send low byte of VDP RAM write address
       swpb r0
       ori  r0,>4000                   ; Set read/write bits 14 and 15 to write (01)
       movb r0,@vdpwa                  ; Send high byte of VDP RAM write address
vsmw_1:
       movb r1,@vdpwd                  ; Write byte to VDP RAM
       dec  r2
       jne  vsmw_1
       rt
*// vsmw

*********************************************************************
*
* VDP Multiple Bytes Write
*
* R0: Write address in VDP RAM
* R1: Source address in CPU RAM
* R2: Number of bytes to write
*
* R0 is modified, but can be restored with: ANDI R0,>3FFF
*
vmbw:
       swpb r0
       movb r0,@vdpwa                  ; Send low byte of VDP RAM write address
       swpb r0
       ori  r0,>4000                   ; Set read/write bits 14 and 15 to write (01)
       movb r0,@vdpwa                  ; Send high byte of VDP RAM write address
vmbw_1:
       movb *r1+,@vdpwd                  ; Write byte to VDP RAM
       dec  r2
       jne  vmbw_1
       rt
*// vmbw

***************************************************************************
*
* Check key
*
* r0: Column in MSB: >0000 - >0700, Row in LSB: >0006 - >0014
*
*       Column   0      1    2    3    4    5     6       7
*     Row
*     >0006      =      .    ,    M    N    /    Fire    Fire
*     >0008    Space    L    K    J    H    ;    Left    Left
*     >000A    Enter    O    I    U    Y    P    Right   Right
*     >000C             9    8    7    6    0    Down    Down
*     >000E    Fctn     2    3    4    5    1    Up      Up
*     >0010    Shift    S    D    F    G    A
*     >0012    Ctrl     W    E    R    T    Q
*     >0014             X    C    V    B    Z
*
* On return NEQ means that the key is pressed
*
KEY_FI EQU  >0606
KEY_LF EQU  >0608
KEY_RG EQU  >060A
KEY_DN EQU  >060C
KEY_UP EQU  >060E
KEY_CM EQU  >0206
KEY_FS EQU  >0106
KEY_0  EQU  >050C
KEY_1  EQU  >050E
KEY_2  EQU  >010E
KEY_3  EQU  >020E
KEY_4  EQU  >030E
KEY_5  EQU  >040E
KEY_6  EQU  >040C
KEY_7  EQU  >030C
KEY_8  EQU  >020C
KEY_9  EQU  >010C
KEY_A  EQU  >0510
KEY_B  EQU  >0414
KEY_C  EQU  >0214
KEY_D  EQU  >0210
KEY_E  EQU  >0212
KEY_F  EQU  >0310
KEY_G  EQU  >0410
KEY_H  EQU  >0408
KEY_I  EQU  >020A
KEY_J  EQU  >0308
KEY_K  EQU  >0208
KEY_L  EQU  >0108
KEY_M  EQU  >0306
KEY_N  EQU  >0406
KEY_O  EQU  >010A
KEY_P  EQU  >050A
KEY_Q  EQU  >0512
KEY_R  EQU  >0312
KEY_S  EQU  >0110
KEY_T  EQU  >0412
KEY_U  EQU  >030A
KEY_V  EQU  >0314
KEY_W  EQU  >0112
KEY_X  EQU  >0114
KEY_Y  EQU  >040A
KEY_Z  EQU  >0514
KEY_SP EQU  >0008
KEY_EN EQU  >000A
KEY_FN EQU  >000E
KEY_SH EQU  >0010
KEY_CT EQU  >0012
JOY_FI EQU  >0606
JOY_LT EQU  >0608
JOY_RT EQU  >060A
JOY_DN EQU  >060C
JOY_UP EQU  >060E
*
check_key:
       li   r12,>0024                  ; CRU address of the column decoder
       ldcr r0,3                       ; Select column
       andi r0,>00ff
       mov  r0,r12                     ; Select row
       tb   0                          ; Test key, EQ if not pressed
       rt
*// check_key

***************************************************************************
*
* Check quit
*
check_quit:
       clr  r1                         ; Test column 0
       li   r12,>0024                  ; Address for column selection
       ldcr r1,3                       ; Select column
       li   r12,>0006                  ; Address to read rows
       stcr r1,8
       andi r1,>1100
       jeq  quit
*      Return
       rt
*      Quit
quit   clr  @>83c4                     ; Reset user ISR address
       blwp @>0000
*// check_quit

***************************************************************************
*
* Data
*
w_map_width:
       data map_width
w_map_height:
       data map_height
w_coll_map_bank_height:
       data coll_map_bank_height

sprite_patterns:
       copy "sprite-patterns.a99"
sprite_attributes:
       byte >20,>48,>00,>0e

**
* Includes
*
normals:
       copy "normals.a99"
map:
       copy "map.a99"

       end
