*********************************************************************************
*
* Send speak external command and first 8 bytes
*
* r0: index of speech data
*
speak:
       .proc
       mov  @speech_detected,r1
       jeq  speak_1
       mov  @speech_counter,r2         ; Get counter
       jne  speak_1                    ; Skip if already speaking
       sla  r0,2                       ; index * 4
       ai   r0,>6000
       seto @speech_bank
       mov  *r0+,@speech_addr
       mov  *r0,@speech_counter
*      Speak external command
       movb @h60,@spchwt
       bl   @delay_12us
*      Send first 8 bytes
       bl   @send_bytes_to_speech
*      Lower music/sfx volume
       li   r0,attenuation_during_speech*256
       mov  r0,@extra_attenuation
speak_1:
       .endproc
*// speak

*********************************************************************************
*
* Send up to 8 bytes to the speech synthesizer
*
speech_player:
       .proc
       mov  @speech_detected,r0
       jeq  speech_player_2
       mov  @speech_counter,r2         ; Get counter
       jeq  speech_player_2            ; Nothing to do
*      Check speech status
       bl   @speech_read_pad
       movb @speech_status,r0
       coc  @h4000,r0                  ; Is speech buffer half empty?
       jne  speech_player_2
*      Speech buffer low
       bl   @send_bytes_to_speech
speech_player_2:
       .endproc
*// speech_player

*********************************************************************************
*
* Send bytes to speech synth
*
send_bytes_to_speech:
       seto @speech_bank
       mov  @speech_addr,r1            ; Get speech data address
       mov  @speech_counter,r2         ; Get counter
       ci   r2,8                       ; More than 8 bytes left?
       jle  send_bytes_to_speech_1     ; No - jump
       li   r2,8                       ; Yes - send 8
send_bytes_to_speech_1:
       movb *r1+,@spchwt               ; Send a byte
       dec  @speech_counter
       dec  r2
       jne  send_bytes_to_speech_1
       mov  r1,@speech_addr            ; Save the new address
       mov  @speech_counter,r2
       jgt  send_bytes_to_speech_2
       clr  @extra_attenuation           ; Normal music/sfx volume
send_bytes_to_speech_2:
       rt
*// send_bytes_to_speech

*********************************************************************************
*
* Check if the Speech Synthesizer is attached
*
speech_check:
       .proc
       clr  r0                         ; Location 0
       bl   @load_speech_address       ; Load addeess
       bl   @delay_42us
       movb @h10,@spchwt
       bl   @delay_12us
       bl   @speech_read_pad           ; Read the byte
       cb   @speech_status,@hAA        ; Is is >AA?
       jeq  speech_check_1             ; Yes. The peripheral is attached
*      Not there
       clr  @speech_detected
       jmp  speech_check_2
*      There
speech_check_1:
       seto @speech_detected
speech_check_2:
       .endproc
*// speech_check

*********************************************************************************
*
* Load address
*
* r0: address
*
* Uses rO, r1, and r2
*
load_speech_address:
       li   r2,4
load_speech_address_1:
       src  r0,4
       mov  r0,r1
       src  r1,4
       andi r1,>0F00                   ; Pick off four bits
       ori  r1,>4000                   ; Make it >4X
       movb r1,@spchwt
       dec  r2                         ; Do it four times
       jne  load_speech_address_1
       li   r1,>4000                   ; Write the >40
       movb r1,@spchwt
       rt
*// load_speech_address

*********************************************************************************
*
* 12 us delay
*
delay_12us:
       nop                             ; Short delay
       nop
       rt
*// delay_12us

*********************************************************************************
*
* 42 us delay
*
delay_42us:
       li   r1,10                      ; Long delay after address set up
delay_42us_1:
       dec  r1
       jne  delay_42us_1
       rt
*// delay_42us

*********************************************************************************
*
* Speech status read routine code, must be copied to scratchpad
*
speech_read:
       movb @spchrd,@speech_status
       nop
       nop
       nop
       rt
speech_read_length:
       equ $-speech_read
*// speech_read

*********************************************************************************
*
* Copy speech status read routine code to scratchpad
*
copy_speech_read_to_pad:
       li   r0,speech_read
       li   r1,speech_read_pad
       li   r2,speech_read_length
copy_speech_read_to_pad_1:
       mov  *r0+,*r1+
       dect r2
       jne  copy_speech_read_to_pad_1
       rt
*// copy_speech_read_to_pad

**
* Constants
h8000  data >8000
h4000  data >4000
h2000  data >2000
h10    byte >10
h60    byte >60
hAA    byte >aa

       even
